pkgname=c-ares
pkgver=1.11.0
pkgrel=1
pkgdesc='C library that performs DNS requests and name resolves asynchronously'
arch=('i686' 'x86_64')
url='http://c-ares.haxx.se/'
license=('custom')
depends=('glibc')
source=("http://c-ares.haxx.se/download/$pkgname-$pkgver.tar.gz"{,.asc}
        'LICENSE')
sha256sums=('b3612e6617d9682928a1d50c1040de4db6519f977f0b25d40cf1b632900b3efd'
            'SKIP'
            '95ba49c119798dae0116dbb285f337f282446c2eea46ad9a3d4c47fb0c71b4e0')
groups=(custom)

build() {
  cd "$pkgname-$pkgver"

  ./configure --prefix=/usr --enable-shared
  make
}

package() {
  cd "$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install
  install -Dm644 "$srcdir/LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
